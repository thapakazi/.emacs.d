#+TITLE: Org file for my org mode config
#+DATE: Thursday, Jan 21 2016
#+DESCRIPTION: Since everything is orgified, so I need this seperate file
#+STARTUP: content

* Org mode 

  One mode to rule them all. 

  Truly the way to [[http://orgmode.org/][live life in plain text]]. I mainly use it to take
  notes and save executable source blocks. I'm also starting +to make  use of its agenda, timestamping and+ capturing features.

  It goes without saying that I also use it to manage my Emacs config.

** Org activation bindings

  Set up some global key bindings that integrate with Org Mode features.

 #+begin_src emacs-lisp
 (bind-key "C-c l" 'org-store-link)
 (bind-key "C-c c" 'org-capture)
 (bind-key "C-c a" 'org-agenda)
 #+end_src

*** COMMENT Org agenda

 Learned about [[https://github.com/sachac/.emacs.d/blob/83d21e473368adb1f63e582a6595450fcd0e787c/Sacha.org#org-agenda][this =delq= and =mapcar= trick from Sacha Chua's config]].

 #+begin_src emacs-lisp
 (setq org-agenda-files
       (delq nil
             (mapcar (lambda (x) (and (file-exists-p x) x))
                     '("~/Dropbox/Agenda"))))
 #+end_src

 *** Org capture

 #+begin_src emacs-lisp
 (bind-key "C-c c" 'org-capture)
 (setq org-default-notes-file "~/Dropbox/Notes/notes.org")
 #+end_src
** Org setup

 Speed commands are a nice and quick way to perform certain actions
 while at the beginning of a heading. It's not activated by default.

 See the doc for speed keys by checking out [[elisp:(info%20"(org)%20speed%20keys")][the documentation for  speed keys in Org mode]].

 #+begin_src emacs-lisp
 (setq org-use-speed-commands t)
 #+end_src

 #+begin_src emacs-lisp
 (setq org-image-actual-width 550)
 #+end_src
** Org tags
 Ok brother I follow you here, as it is...
 
 The default value is -77, which is weird for smaller width windows.
 I'd rather have the tags align horizontally with the header. 45 is a
 good column number to do that.

 #+begin_src emacs-lisp
 (setq org-tags-column 45)
(require 'org-mouse)
 #+end_src
** Org babel for golang
   #+BEGIN_SRC emacs-lisp
   (use-package ob-go
     :ensure t
     )
   #+END_SRC
** Org babel languages

  #+begin_src emacs-lisp
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((python . t)
       (C . t)
       (calc . t)
       (latex . t)
       (java . t)
       (ruby . t)
       (scheme . t)
       (shell . t)
       (sqlite . t)
       (go . t)
       (js . t)))

    ;; (defun my-org-confirm-babel-evaluate (lang body)
    ;;   "Do not confirm evaluation for these languages."
    ;;   (not (or (string= lang "C")
    ;; 	   (string= lang "java")
    ;; 	   (string= lang "python")
    ;; 	   (string= lang "sqlite"))))
    ;; (setq org-confirm-babel-evaluate 'my-org-confirm-babel-evaluate)
  #+end_src
** Org babel/source blocks

  I like to have source blocks properly syntax highlighted and with the
  editing popup window staying within the same window so all the windows
  don't jump around. Also, having the top and bottom trailing lines in
  the block is a waste of space, so we can remove them.

  I noticed that fontification doesn't work with markdown mode when the
  block is indented after editing it in the org src buffer---the leading
  #s for headers don't get fontified properly because they appear as Org
  comments. Setting ~org-src-preserve-indentation~ makes things
  consistent as it doesn't pad source blocks with leading spaces.

  #+begin_src emacs-lisp
  (setq org-src-fontify-natively t
        org-src-window-setup 'current-window
        org-src-strip-leading-and-trailing-blank-lines t
        org-src-preserve-indentation t
        org-src-tab-acts-natively t)
  #+end_src
** Org exporting
*** COMMENT Pandoc exporter                  :fixme: :future:

 Pandoc converts between a huge number of different file formats. 

 #+begin_src emacs-lisp
 (use-package ox-pandoc
   :no-require t
   :ensure t)
 #+end_src
** COMMENT Org old config
 Old good configs for org

#+BEGIN_SRC emacs-lisp
;;======================================================================
;; ORG-MODE

(add-hook 'org-mode-hook 'turn-on-auto-fill)

;; TODO: org -mode image zoom
;; (setq org-image-actual-width 800)

(add-hook 'org-mode-hook
          '(lambda ()
             (setq org-file-apps
                   '((auto-mode . emacs)
                     ("\\.jpg\\'" . "feh %s")
                     ("\\.png\\'" . "feh %s")
                     ("\\.mkv\\'" . "mplayer %s")
                     ("\\.mp4\\'" . "mplayer %s")
		             ("\\.avi\\'" . "mplayer %s")
                     ("\\.pdf\\'" . "evince %s")))))

;; removing the C-j bind for goto-last-change, of el-get
(add-hook 'org-mode-hook
          '(lambda ()
             (define-key org-mode-map (kbd "C-j") nil)))

;;----------------------------------------------------------------------
;; Sort list by checkbox type
;; http://orgmode.org/worg/org-hacks.html

(defun org-sort-list-by-checkbox-type ()
  "Sort list items according to Checkbox state."
  (interactive)
  (org-sort-list
   nil ?f
   (lambda ()
     (if (looking-at org-list-full-item-re)
         (cdr (assoc (match-string 3)
                     '(("[X]" . 1) ("[-]" . 2) ("[ ]" . 3) (nil . 4))))
       4))))

;;----------------------------------------------------------------------
;; org put checkbox if previous is checkbox

(defun org-i-need-checkbox ()
  (interactive)
  (previous-line)
  (setq previous_line
        (buffer-substring
         (line-beginning-position) (line-end-position)))
  (next-line)
  (when (string-match "- \\[" previous_line)
    (insert "[ ] "))
  (insert "haoeush")
  )

;; (add-hook 'org-meta-return 'org-i-need-checkbox)


;;----------------------------------------------------------------------
;; Add dot after headline
;; https://yoo2080.wordpress.com/2013/08/24/changing-the-number-format-for-section-headings-in-org-mode-html-export/
(defun my-html-filter-headline-yesdot (text backend info)
  "Ensure dots in headlines."
  (when (org-export-derived-backend-p backend 'html)
    (save-match-data
      (when (let ((case-fold-search t))
              (string-match (rx (group "<span class=\"section-number-" (+ (char digit)) "\">"
                                       (+ (char digit ".")))
                                (group "</span>"))
                            text))
        (replace-match "\\1.\\2"
                       t nil text)))))

(eval-after-load 'ox
  '((point)
     (add-to-list 'org-export-filter-headline-functions
                  'my-html-filter-headline-yesdot)))


                                        ; Setting Colours (faces) for todo states to give clearer view of work 
(setq org-todo-keyword-faces
      '(("TODO" . org-warning)
        ("DOING" . "yellow")
        ("BLOCKED" . "red")
        ("REVIEW" . "orange")
        ("DONE" . "green")
        ("ARCHIVED" .  "blue")
        ("FUTURE" .  "#EE82EE")))


#+END_SRC
** org-bullets
    #+begin_src emacs-lisp
(use-package org-bullets
  :ensure t
  :config
  (add-hook 'org-mode-hook (lambda () (org-bullets-mode 1)))
  )
    #+end_src
** [[https://orgmode.org/manual/Easy-templates.html][org-easy-templates]]

   As my org-version is 9.2
   #+begin_src emacs-lisp
   org-version
   #+end_src

   #+RESULTS:
   : 9.2

   and I am used to ~<s TAB~ folow for generating babel block, to get
   the key binding:
   
   video: https://www.youtube.com/watch?v=u46CL0-gVTU
   
   #+begin_src emacs-lisp
   (require 'org-tempo)
   #+end_src
** Org mouse
   Better mouse support for org-mode: [[http://orgmode.org/w/?p=org-mode.git;a=blob_plain;f=lisp/org-mouse.el;hb=HEAD][org-mouse]]
   Its loaded from [[./custom-load-list/org-mouse.el][custom-load-list-dir]]

** TODO Org capture
   Org capture kungfu configs
   #+BEGIN_SRC emacs-lisp
;; org-capture
(setq org-capture-templates
      (quote (("t" "todo" entry (file (concat org-directory "/todos.org"))
               "* TODO %?\n%U\n%a\n" :clock-in t :clock-resume t)
              ("n" "note" entry (file (concat org-directory "/notes.org"))
               "* %? :NOTE:\n%U\n%a\n" :clock-in t :clock-resume t)
              ("j" "Journal"
	       entry (file (get-journal-file-today))
	       "* Event: %?\n\n  %i\n\n  From: %a"
	       :empty-lines 1)
              ("y" "youtube-me" entry (file (concat org-directory "/youtube-dl.org"))  "* %?")
              ("o" "CloudFactory" entry (file+datetree (concat org-directory "/cloudfactory.org"))
               "* %?\n%U\n" :clock-in t :clock-resume t)
              ("d" "CloudFactory Docs " entry (file (concat org-directory "/cloudfactory-docs.org"))
               "* %?")
              ("h" "Hisab Kitab " entry (file+datetree+prompt (concat org-directory "/hisab-kitab.org")) "* %?")
              ("x" "What on my plate today ?? " entry (file+datetree+prompt (concat org-directory "/devops.org"))
               "* TODO %?")
              ("g" "awesome tools " entry (file (concat org-directory "/awesome_tools.org"))
               "* %?")
              ("c" "Coursera Download List" entry (file (concat org-directory "/coursera-dl.org"))
               "* [ ]  %?")
              )))

;; org-caputre goodies
;; http://www.howardism.org/Technical/Emacs/journaling-org.html
(defun get-journal-file-today ()
  "Return filename for today's journal entry."
  (let ((daily-name (format-time-string "%Y%m%d")))
    (expand-file-name (concat org-journal-dir daily-name))))

(defun journal-file-today ()
  "Create and load a journal file based on today's date."
  (interactive)
  (find-file (get-journal-file-today)))

(global-set-key (kbd "C-c f j") 'journal-file-today)


(defun journal-file-insert ()
  "Insert's the journal heading based on the file's name."
  (interactive)
  (when (string-match "\\(20[0-9][0-9]\\)\\([0-9][0-9]\\)\\([0-9][0-9]\\)"
                      (buffer-name))
    (let ((year  (string-to-number (match-string 1 (buffer-name))))
          (month (string-to-number (match-string 2 (buffer-name))))
          (day   (string-to-number (match-string 3 (buffer-name))))
          (datim nil))
      (setq datim (encode-time 0 0 0 day month year))
      (insert (format-time-string
               "#+TITLE: Journal Entry- %Y-%b-%d (%A)\n\n" datim)))))

   #+END_SRC

* blog with org and hugo
  All the things are described here: [[https://thapakazi.github.io/posts/partial_publish/#org85703f5][History of it all]]
** Requirements
*** Define content dir
    #+begin_src emacs-lisp
(defvar hugo-content-dir
  (concat user-emacs-directory "blog/posts/")
  ;; (getenv "HUGO_CONTENT_PROJECT_DIR")
  "Path to Hugo's content directory"
  )
    #+end_src

    #+RESULTS:
    : hugo-content-dir

*** Github flavoured markdown
   #+begin_src emacs-lisp
(use-package ox-gfm
  :ensure t
  :no-require t
)
   #+end_src

   #+RESULTS:

** Hugo properties
   #+begin_src emacs-lisp
;; This is GPLv2. If you still don't know the details, read
;; http://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html
;; copied verbatium from http://www.holgerschurig.de/en/emacs-blog-from-org-to-hugo/


(defun hugo-ensure-property (property)
  "Make sure that a property exists. If not, it will be created.

Returns the property name if the property has been created,
otherwise nil."
  (if (org-entry-get nil property)
      nil
    (progn (org-entry-put nil property "")
           property)))

(defun hugo-ensure-properties ()
  "This ensures that several properties exists. If not, these
properties will be created in an empty form. In this case, the
drawer will also be opened and the cursor will be positioned
at the first element that needs to be filled.

Returns list of properties that still must be filled in"
  (require 'dash)
  (let ((current-time (format-time-string (org-time-stamp-format t t) (org-current-time)))
        first)
    (save-excursion
      (unless (org-entry-get nil "TITLE")
        (org-entry-put nil "TITLE" (nth 4 (org-heading-components))))
      (setq first (--first it (mapcar #'hugo-ensure-property '("HUGO_TAGS" "HUGO_TOPICS" "HUGO_FILE"))))
      (unless (org-entry-get nil "HUGO_DATE")
        (org-entry-put nil "HUGO_DATE" current-time)))
    (when first
      (goto-char (org-entry-beginning-position))
      ;; The following opens the drawer
      (forward-line 1)
      (beginning-of-line 1)
      (when (looking-at org-drawer-regexp)
        (org-flag-drawer nil))
      ;; And now move to the drawer property
      (search-forward (concat ":" first ":"))
      (end-of-line))
    first))
   #+end_src
** Hugo export properties Function
   #+begin_src emacs-lisp
;; This is GPLv2. If you still don't know the details, read
;; http://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html

(defun hugo ()
  (interactive)
  (unless (hugo-ensure-properties)
    (let* ((title    (concat "title = \"" (org-entry-get nil "TITLE") "\"\n"))
           (date     (concat "date = \"" (format-time-string "%Y-%m-%d" (apply 'encode-time (org-parse-time-string (org-entry-get nil "HUGO_DATE"))) t) "\"\n"))
           (topics   (concat "topics = [ \"" (mapconcat 'identity (split-string (org-entry-get nil "HUGO_TOPICS") "\\( *, *\\)" t) "\", \"") "\" ]\n"))
           (tags     (concat "tags = [ \"" (mapconcat 'identity (split-string (org-entry-get nil "HUGO_TAGS") "\\( *, *\\)" t) "\", \"") "\" ]\n"))
           (fm (concat "+++\n"
                       title
                       date
                       tags
                       topics
                       "+++\n\n"))
           (file     (org-entry-get nil "HUGO_FILE"))
           (coding-system-for-write buffer-file-coding-system)
           (backend  'md)
           (blog))
      ;; try to load org-mode/contrib/lisp/ox-gfm.el and use it as backend
      (if (require 'ox-gfm nil t)
          (setq backend 'gfm)
        (require 'ox-md))
      (setq blog (org-export-as backend t))
      ;; Normalize save file path
      (unless (string-match "^[/~]" file)
        (setq file (concat hugo-content-dir file))
        (unless (string-match "\\.md$" file)
          (setq file (concat file ".md")))
        ;; save markdown
        (with-temp-buffer
          (insert fm)
          (insert blog)
          (untabify (point-min) (point-max))
          (write-file file)
          (message "Exported to %s" file))
        ))))
   #+end_src
** config
   #+begin_src emacs-lisp
(bind-key "M-g h" #'hugo)
   #+end_src


* Org Roam to rescue
  Org roam to rescue

  #+begin_src emacs-lisp
(use-package org-roam
  :ensure t
  :init
  (setq org-roam-v2-ack t)
  :custom
  (org-roam-directory (file-truename "~/repos/thapakazi/orgr"))
  (org-roam-completion-everywhere t)
  :bind (("C-c n l" . org-roam-buffer-toggle)
         ("C-c n f" . org-roam-node-find)
         ("C-c n g" . org-roam-graph)
         ("C-c n i" . org-roam-node-insert)
         ("C-c n c" . org-roam-capture)
         :map org-mode-map
         ("C-M-i" . completion-at-point)
         ;; Dailies
         ("C-c n j" . org-roam-dailies-capture-today))
  ("C-c n d" . org-roam-dailies-map)
  :config
  (org-roam-db-autosync-mode)
  ;; If using org-roam-protocol
  (require 'org-roam-protocol)
  (org-roam-setup)
  )
  #+end_src

  #+RESULTS:
  : t
